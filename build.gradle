plugins {
	id 'org.springframework.boot' version '3.5.10'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'java'
	id 'com.github.spotbugs' version '6.1.2'
	id 'org.owasp.dependencycheck' version '12.1.3'
}

group = 'it.gov.innovazione'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '21'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation('org.springframework.boot:spring-boot-starter-web') {
		exclude group: 'org.yaml'
		exclude group: 'com.fasterxml.jackson.core'
	}
	compileOnly 'org.projectlombok:lombok'
	implementation 'org.owasp.encoder:encoder:1.4.0'
	implementation 'commons-io:commons-io:2.18.0'
	implementation 'net.sf.saxon:Saxon-HE:12.5'
	annotationProcessor 'org.projectlombok:lombok'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
	useJUnitPlatform()
}

spotbugs {
	toolVersion = '4.8.6'
}

spotbugsMain {
	excludeFilter = file("${rootProject.projectDir}/config/spotbugs/exclude-filter.xml")

	reports {
		html {
			required = true
			outputLocation = file("${layout.buildDirectory.get()}/reports/spotbugs/main/spotbugs.html")
		}
	}
}

spotbugsTest {
	excludeFilter = file("${rootProject.projectDir}/config/spotbugs/exclude-filter.xml")
	reports {
		html {
			required = true
			outputLocation = file("${layout.buildDirectory.get()}/reports/spotbugs/test/spotbugs.html")
		}
	}
}

dependencyCheck {
	nvd {
		datafeedUrl = "https://dependency-check.github.io/DependencyCheck_Builder/nvd_cache"
		validForHours = 24
	}
	failOnError = false
	failBuildOnCVSS = 7
	analyzers {
		assemblyEnabled = false
		ossIndexEnabled = false
	}
	suppressionFile = "${rootProject.projectDir}/config/dependency-check/dependency-check-known-issues.xml"
}

bootJar {
	enabled = true
	archiveFileName.set("lode.jar")
}
